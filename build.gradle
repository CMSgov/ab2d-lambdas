plugins {
    id 'java'
    id 'com.jfrog.artifactory' version '4.29.0' apply false
    id "org.sonarqube" version "3.4.0.2513"
    id 'org.cyclonedx.bom' version '1.7.2' apply true
}

version = "1.0"
group = "gov.cms.ab2d"

ext {
    sourcesRepo = 'ab2d-main'
    artifactoryLoc = project.hasProperty('artifactory_contextUrl') ? project.artifactory_contextUrl
            : System.getenv()['ARTIFACTORY_URL']

    // Override user and password
    artifactory_user = project.hasProperty('artifactory_user') ? project.artifactory_user
            : System.getenv()['ARTIFACTORY_USER']
    artifactory_password = project.hasProperty('artifactory_password') ? project.artifactory_password
            : System.getenv()['ARTIFACTORY_PASSWORD']

    metricsVersion = '1.0.0'
    fetcherVersion = '1.0.0'


    sourcesRepo = 'ab2d-maven-repo'
    deployerRepo = 'ab2d-main'
    resolverRepo = 'ab2d-main'
}


repositories {
    mavenCentral()
    maven {
        url = "${artifactoryLoc}/${sourcesRepo}"
        credentials {
            username = project.artifactory_user
            password = project.artifactory_password
        }
    }
}

test {
    useJUnitPlatform()
}

subprojects {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    apply plugin: "org.cyclonedx.bom"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact file("build/distributions/${project.name}.zip")
            }
        }
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    group 'gov.cms.ab2d'


    artifactory {
        contextUrl = project.artifactoryLoc

        publish {
            repository {
                repoKey = "${deployerRepo}"
                username = project.artifactory_user
                password = project.artifactory_password
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = false
            }
        }
        resolve {
            repository {
                repoKey = "${resolverRepo}"
                username = project.artifactory_user
                password = project.artifactory_password
                maven = true
            }
        }
    }
}

dependencies {
    implementation "org.cyclonedx:cyclonedx-gradle-plugin:1.4.0"
    implementation "org.cyclonedx:cyclonedx-core-java:5.0.4"
}

allprojects {
    cyclonedxBom {
        // includeConfigs is the list of configuration names to include when generating the BOM (leave empty to include every configuration)
        includeConfigs = ["runtimeClasspath"]
        // skipConfigs is a list of configuration names to exclude when generating the BOM
        skipConfigs = ["compileClasspath", "testCompileClasspath"]
        // Specified the type of project being built. Defaults to 'library'
        projectType = "library"
        // Specified the version of the CycloneDX specification to use. Defaults to 1.4.
        schemaVersion = "1.4"
        // Boms destination directory (defaults to build/reports)
        destination = file("build/reports")
        // The file name for the generated BOMs (before the file format suffix). Defaults to 'bom'
        outputName = "bom"
        // The file format generated, can be xml, json or all for generating both
        outputFormat = "all"
        // Exclude BOM Serial Number
        includeBomSerialNumber = false
        // Override component version
        componentVersion = "2.0.0"
    }

    task lookForArtifacts {
        doLast {
            //This is the parent build where nothing gets published. Skip it.
            if (project.name != 'ab2d-lambdas')
            //Set path to repository that might exist if previous published on this version.
            //Sets project name and if it exists in repository. Print out so jenkins can take it.
                System.out.print("'''" + project.name + ":" + urlExists("${artifactoryLoc}/${deployerRepo}/gov/cms/ab2d/${project.name}/${project.version}/${project.name}-${project.version}.zip"))
        }
    }

    repositories {
        maven {
            url = "${artifactoryLoc}/${sourcesRepo}"
            credentials {
                username = project.artifactory_user
                password = project.artifactory_password
            }
        }
        mavenCentral()
    }


}

def getBase64EncodedCredentials() {
    def s = "$artifactory_user" + ":" + "$artifactory_password"
    return s.bytes.encodeBase64().toString()
}

//Check if url exist in the repository
def urlExists(repositoryUrl) {
    try {
        def connection = (HttpURLConnection) new URL(repositoryUrl).openConnection()

        connection.setRequestProperty("Authorization", "Basic " + getBase64EncodedCredentials())
        connection.setConnectTimeout(10000)
        connection.setReadTimeout(10000)
        connection.setRequestMethod("HEAD")

        def responseCode = connection.getResponseCode()

        if (responseCode == 401) {
            throw new RuntimeException("Unauthorized MavenUser user. Please provide valid username and password.")
        }

        return (200 == responseCode)

    } catch (IOException ignored) {
        println(ignored)
        return false
    }
}

task buildZip(type: Zip) {
    dependsOn ':metrics-lambda:buildZip'
    dependsOn ':eob-fetcher:buildZip'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

build.dependsOn buildZip


