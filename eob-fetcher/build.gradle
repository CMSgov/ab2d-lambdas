plugins {
    id 'java'
    id 'com.jfrog.artifactory' version '4.29.0' apply false
    id "org.sonarqube" version "3.4.0.2513"
}

ext {
    sourcesRepo = 'ab2d-main'
    artifactoryLoc = project.hasProperty('artifactory_contextUrl') ? project.artifactory_contextUrl
            : System.getenv()['ARTIFACTORY_URL']

    // Override user and password
    artifactory_user = project.hasProperty('artifactory_user') ? project.artifactory_user
            : System.getenv()['ARTIFACTORY_USER']
    artifactory_password = project.hasProperty('artifactory_password') ? project.artifactory_password
            : System.getenv()['ARTIFACTORY_PASSWORD']
}

repositories {
    mavenCentral()
    maven {
        url = "${artifactoryLoc}/${sourcesRepo}"
        credentials {
            username = project.artifactory_user
            password = project.artifactory_password
        }
    }
}

dependencies {
    compileOnly    'org.projectlombok:lombok:1.18.24'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'
    implementation 'org.springframework:spring-context:5.3.20'
    implementation 'com.newrelic.agent.java:newrelic-api:7.6.0'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:5.7.2'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.7.2'
    implementation 'gov.cms.ab2d:ab2d-aggregator:1.2.1'
    implementation 'gov.cms.ab2d:ab2d-bfd:1.3.1'
    implementation 'gov.cms.ab2d:ab2d-events-client:1.3.1'
    implementation 'gov.cms.ab2d:ab2d-fhir:1.1.1'
    implementation 'gov.cms.ab2d:ab2d-filters:1.6.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3'
    annotationProcessor  "org.projectlombok:lombok:1.18.24"
}

test {
    useJUnitPlatform()
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

build.dependsOn buildZip
